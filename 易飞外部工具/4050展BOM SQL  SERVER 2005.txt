-- =============================================
-- 展开BOM(多阶 不含替代料)
-- =============================================
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[SZPro_ExpandEndBom]') AND OBJECTPROPERTY(id,N'IsProcedure') = 1)
BEGIN
	DROP PROCEDURE SZPro_ExpandEndBom
END
go
CREATE procedure [dbo].[SZPro_ExpandEndBom]
@bInvCode varchar(32),  --起始品号
@eInvCode varchar(32),  --终止品号
@InvList  nvarchar(2000)  --品号清单,用逗号隔开,如:1001,1002,1003
AS
BEGIN
	SET NOCOUNT ON
	SET @bInvCode = ISNULL(@bInvCode,'')
	SET @eInvCode = ISNULL(@eInvCode,'')
	SET @InvList = ISNULL(@InvList,'')
	--保存符合条件的BOM主件
	CREATE TABLE #InvTable
	(
	   cInvCode VARCHAR(32) NOT NULL PRIMARY KEY
	)
	
	DECLARE @sql NVARCHAR(4000)
	SET @sql ='insert into #InvTable(cInvCode)
				SELECT MC001 FROM BOMMC WHERE ('''+ISNULL(@bInvCode,'')+'''='''' OR MC001>='''+@bInvCode+''')
				AND ('''+ISNULL(@eInvCode,'')+'''='''' OR MC001<='''+@eInvCode+''')
				AND ('''+ISNULL(@InvList,'')+'''='''' OR MC001 IN ('''+REPLACE(@InvList,',',''',''')+'''))'
	EXEC(@sql);
			
	WITH Boms
    AS
    (
		SELECT a.MC001 AS MD001,CAST('' AS CHAR(4)) AS MD002,a.MC001 AS FPInv,a.MC001 AS PInv,0 AS flevel,CAST('0' AS VARCHAR(128)) AS slevel,CAST(dbo.LPad(Row_Number() over (order BY a.MC001 desc),8) AS VARCHAR(1000)) as treepath 
        FROM BOMMC a WHERE MC001 IN(SELECT cInvCode FROM #InvTable)
        UNION ALL
        SELECT d.MD003,d.MD002,p.FPInv,p.MD001,flevel+1,CAST(dbo.LPadWithChar('.',flevel+1,2*(flevel+1)+LEN(flevel+1)) AS VARCHAR(128)),CAST(p.treepath + dbo.LPad(Row_Number() over (order by d.MD001 desc),3) AS VARCHAR(1000)) as treepath
        FROM BOMMD d 
        INNER JOIN Boms p ON d.MD001=p.MD001 
    )
	select slevel,flevel AS [Level],a.FPInv, a.PInv AS MD001,  a.MD002,
	(CASE WHEN b.MB025 = 'P' THEN 'P:采购件' WHEN b.MB025 = 'M' THEN 'M:自制件'
	WHEN b.MB025 = 'S' THEN 'S:委外加工件' WHEN b.MB025 = 'Y' THEN 'Y:虚设件'
	WHEN b.MB025 = 'C' THEN 'C:配置件' ELSE '有错误' END) AS MB025, 
	c.MD003,b.MB002,b.MB003,b.MB004, c.MD006,c.MD007, (c.MD008*100 ) AS MD008,
   (CASE WHEN flevel=0 THEN NULL  WHEN c.MD017 = '1' THEN '1.直接材料' WHEN c.MD017 = '2' THEN '2.间接材料'
	WHEN c.MD017 = '3' THEN '3.供应商供料' WHEN c.MD017 = '4' THEN '4.不发料'
	WHEN c.MD017 = '5' THEN '5.客户供料' ELSE '有错误' END) AS MD017, 
	c.MD019,c.MD010,
	CASE WHEN ISNULL(MD011,'') = '' then '' ELSE CONVERT(VARCHAR(10),CAST(c.MD011 AS DATETIME),120) END AS MD011,
	CASE WHEN ISNULL(MD012,'') = '' then '' ELSE CONVERT(VARCHAR(10),CAST(c.MD012 AS DATETIME),120) END AS MD012,treepath
	FROM  Boms a
	LEFT JOIN dbo.BOMMD c ON a.MD001 = c.MD003 AND a.PInv = c.MD001
	INNER JOIN INVMB b ON a.MD001= b.MB001
	ORDER BY treepath

	DROP TABLE #InvTable
END

GO
-- =============================================
-- 展开BOM 中文列(多阶 不含替代料)
-- =============================================
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = OBJECT_ID(N'[dbo].[SZPro_ExpandEndBomCN]') AND OBJECTPROPERTY(id,N'IsProcedure') = 1)
BEGIN
	DROP PROCEDURE SZPro_ExpandEndBomCN
END
go
CREATE procedure [dbo].[SZPro_ExpandEndBomCN]
@bInvCode varchar(32),  --起始品号
@eInvCode varchar(32),  --终止品号
@InvList  nvarchar(max)  --品号清单,用逗号隔开,如:1001,1002,1003
AS
BEGIN
	SET NOCOUNT ON
	CREATE TABLE #temp
	(
		所处阶次 VARCHAR(128),
		阶次 tinyint,
		主件品号 Nvarchar(20),
		上阶品号 Nvarchar(20),
		序号 Nvarchar(4),
		属性 NVARCHAR(14),
		元件品号 Nvarchar(20),
		品名 NVARCHAR(70),
		规格型号 NVARCHAR(70),
        单位 NVARCHAR(4),
        组成用量 numeric(9,4),
        底数 numeric(9,4),
        损耗率 numeric(9,2),
        材料类型 NVARCHAR(16),
        新插件位置1 NVARCHAR(80),
        取替代料 NVARCHAR(2),
        生效日期 NVARCHAR(12),
        失效日期 NVARCHAR(12),
        路径 Nvarchar(1000)) 
        
    INSERT INTO #temp
	EXEC SZPro_ExpandEndBom @bInvCode,@eInvCode,@InvList

	SELECT * FROM #temp
	DROP TABLE #temp	
END
